# Initialise DOTFILES
[[ -f $HOME/.dot ]] && source $HOME/.dot

echo $HOSTNAME $(date +"%Y-%m-%d %H:%M:%S") $$ $PPID zsh/zsh_rc >> $HOME/.dot_log

# Runs second (after .zprofile) for each shell created in the gnome-session

# Antigen
if [ -f $DOTFILES/externals/antigen/antigen.zsh ]; then
	source "$DOTFILES/externals/antigen/antigen.zsh"

	# Try out prezto instead of oh-my-zsh
	_zdotdir_set=${+parameters[ZDOTDIR]}
	if (( _zdotdir_set  )); then
		_old_zdotdir=$ZDOTDIR
	fi

	antigen use prezto
	antigen bundle sorin-ionescu/prezto modules/helper  # required for Git module
	antigen bundle sorin-ionescu/prezto modules/editor
	antigen bundle sorin-ionescu/prezto modules/git
	antigen bundle sorin-ionescu/prezto modules/prompt

	if (( _zdotdir_set  )); then
		ZDOTDIR=$_old_zdotdir
	else
		unset ZDOTDIR
		unset _old_zdotdir
	fi;
	unset _zdotdir_set

	# Used by some prompts :(
	# antigen bundle virtualenv

	# antigen bundle git
	# antigen bundle svn
	# antigen bundle mvn

	# antigen bundle arialdomartini/oh-my-git
	# antigen bundle seletskiy/zsh-context-aliases

	case $TERM in
		xterm*|screen*)
			antigen bundle zsh-users/zsh-syntax-highlighting
			;;
		vt100)
			;;
		*)
			antigen bundle zsh-users/zsh-syntax-highlighting
			;;
	esac

	antigen bundle zsh-users/zsh-history-substring-search

	# Liquidprompt
	# Add prefix to set window title
	antigen bundle nojhan/liquidprompt
	prompt_tag
	LP_ENABLE_TITLE=1

	# Ruby
	# antigen bundle ruby
	# antigen bundle rvm

	antigen bundle psprint/zsh-cmd-architect

	antigen apply
fi

# oh-my-zsh clobbers LESS :(
export LESS="-F -X -R"

# History substring search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

# Load common functions
if [[ -d $DOTFILES/common/functions ]]
then
	fpath+=( $DOTFILES/common/functions )
	for f in $DOTFILES/common/functions/*
	do
		[[ -f $f ]] && autoload $(basename $f)
	done
fi

# Load zsh specific functions
if [[ -d $DOTFILES/zsh/functions ]]
then
	fpath+=( $DOTFILES/zsh/functions )
	for f in $DOTFILES/zsh/functions/*
	do
		[[ -f $f ]] && autoload $(basename $f)
	done
fi

# turn of nomatch so that stuff like -? and ^. works
setopt nonomatch

# Use dig script to get hosts for completion
if [[ -x $DOTFILES/common/scripts/get-hosts ]]
then
	zstyle -e ':completion:*' hosts 'reply=($($DOTFILES/common/scripts/get-hosts))'
fi

# Extra processing when inside emacs
if [[ -n "$INSIDE_EMACS" ]]
then
	chpwd() { print -P "\033AnSiTc %d" }
	print -P "\033AnSiTu %n"
	print -P "\033AnSiTc %d"
fi

# Load aliases
[[ -f ~/.zsh_alias ]] && . ~/.zsh_alias
