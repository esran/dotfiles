echo $(date +"%Y-%m-%d %H:%M:%S") $$ $PPID zsh/zsh_rc >> $HOME/.dot_log

# Runs second (after .zprofile) for each shell created in the gnome-session

# Initialise DOTFILES
[[ -f $HOME/.dot ]] && source $HOME/.dot

# Antigen
if [ -f $DOTFILES/externals/antigen/antigen.zsh ]; then
	source "$DOTFILES/externals/antigen/antigen.zsh"

	antigen-use oh-my-zsh

	# Used by some prompts :(
	antigen-bundle virtualenv

	antigen-bundle git
	antigen-bundle svn
	antigen-bundle mvn

	antigen-bundle arialdomartini/oh-my-git
	# antigen theme arialdomartini/oh-my-git-themes arialdo-granzestyle

	antigen-bundle seletskiy/zsh-context-aliases

	case $TERM in
		xterm*|screen*)
			antigen-bundle zsh-users/zsh-syntax-highlighting
			antigen theme gnzh
			;;
		vt100)
			;;
		*)
			antigen-bundle zsh-users/zsh-syntax-highlighting
			antigen theme gnzh
			;;
	esac

	antigen-bundle zsh-users/zsh-history-substring-search

	# Ruby
	antigen-bundle ruby
	antigen-bundle rvm

	antigen-apply

	# oh-my-zsh clobbers LESS :(
	export LESS="-F -X -R"
fi

# History substring search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

# Load zsh functions
if [[ -d $DOTFILES/zsh/functions ]]
then
	fpath+=$DOTFILES/zsh/functions
	for f in $DOTFILES/zsh/functions/*
	do
		[[ -f $f ]] && autoload $(basename $f)
	done
fi

# turn of nomatch so that stuff like -? and ^. works
setopt nonomatch

# Use dig script to get hosts for completion
if [[ -x $DOTFILES/common/scripts/get-hosts ]]
then
	zstyle -e ':completion:*' hosts 'reply=($($DOTFILES/common/scripts/get-hosts))'
fi

# Extra processing when inside emacs
if [[ -n "$INSIDE_EMACS" ]]
then
	chpwd() { print -P "\033AnSiTc %d" }
	print -P "\033AnSiTu %n"
	print -P "\033AnSiTc %d"
fi

# Load aliases
[[ -f ~/.zsh_alias ]] && . ~/.zsh_alias

