echo $(date +"%Y-%m-%d %H:%M:%S") $$ $PPID zsh/zsh_rc >> $HOME/.dot_log

# Runs second (after .zprofile) for each shell created in the gnome-session

# Find out true location. Note that this assumes we have actually
# been installed as ~/.zshrc!
if [[ -z $DOTFILES ]]
then
	DOTFILES=$(readlink ~/.zshrc)
	if [[ $? -eq 0 ]]
	then
		DOTFILES=$(dirname $DOTFILES)
		DOTFILES=${DOTFILES%/zsh}
		# Check for a relative path which won't start with a slash
		if [[ -n ${DOTFILES##/*} ]]
		then
			DOTFILES=~/$DOTFILES
		fi
	else
		DOTFILES=~/.dotfiles
	fi
fi

export DOTFILES

# Antigen
if [ -f $DOTFILES/externals/antigen/antigen.zsh ]; then
	source "$DOTFILES/externals/antigen/antigen.zsh"

	antigen-use oh-my-zsh

	# Used by some prompts :(
	antigen-bundle virtualenv

	antigen-bundle git
	antigen-bundle svn
	antigen-bundle mvn

	antigen-bundle arialdomartini/oh-my-git
	# antigen theme arialdomartini/oh-my-git-themes arialdo-granzestyle

	case $TERM in
		xterm*|screen*)
			antigen-bundle zsh-users/zsh-syntax-highlighting
			antigen theme agnoster
			;;
		vt100)
			;;
		*)
			echo "unhandled term type: $TERM"
			;;
	esac

	antigen-bundle zsh-users/zsh-history-substring-search

	# Ruby
	antigen-bundle ruby
	antigen-bundle rvm

	antigen-apply

	# oh-my-zsh clobbers LESS :(
	export LESS="-F -X -R"
fi

# History substring search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

# Load zsh functions
if [[ -d $DOTFILES/zsh/functions ]]
then
	fpath+=$DOTFILES/zsh/functions
	for f in $DOTFILES/zsh/functions/*
	do
		[[ -f $f ]] && autoload $(basename $f)
	done
fi

# Add any functions from my scripts/zsh folder (TODO: legacy, remove this)
if [ -d ~/scripts/zsh ]
then
	fpath+=~/scripts/zsh
	for f in ~/scripts/zsh/*
	do
		[ -f $f ] && autoload $(basename $f)
	done
fi

# PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting
set path+=~/.rvm/bin

# Solarized dircolors
if [[ -f $DOTFILES/externals/dircolors-solarized/dircolors.ansi-light ]]
then
	eval `dircolors $DOTFILES/externals/dircolors-solarized/dircolors.ansi-light`
fi

# turn of nomatch so that stuff like -? and ^. works
setopt nonomatch

# Use dig script to get hosts for completion
if [[ -x $DOTFILES/common/scripts/get-hosts ]]
then
	zstyle -e ':completion:*' hosts 'reply=($($DOTFILES/common/scripts/get-hosts))'
fi

# Load aliases
[[ -f ~/.zsh_alias ]] && . ~/.zsh_alias

